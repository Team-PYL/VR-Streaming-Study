// This is a configuration file used to determine all the projects prior to
// building them.

// Explicitly load all the SDK samples using their full path.
include ':samples:sdk-controllerclient'
include ':samples:sdk-simplepanowidget'
include ':samples:sdk-simplevideowidget'
include ':samples:sdk-treasurehunt'
include ':samples:sdk-videoplayer'

// NDK samples are disabled by default because they need the Android NDK to be
// installed in addition to the SDK. To build them, first extract the NDK .so
// files by running "./gradlew :extractNdk" which will create a libraries/jni
// folder. Then uncomment these lines to enable the NDK samples and synchronize
// Android Studio.
//include ':samples:ndk-controllerpaint'
//include ':samples:ndk-treasurehunt'

// Android Studio doesn't preserve nested modules in its Project View. To create
// a cleaner layout, we reorganize and rename the modules loaded above.
Set moduleList = new HashSet()  // Modules to display in the Project View.

// Move all the modules from root/samples/simplepanowidget to
// root/samples-simplepanowidget.
rootProject.children.each { outerDir ->
    outerDir.children.each { innerDir ->
        // Rename the modules as we move them around.
        innerDir.name = "$outerDir.name-$innerDir.name"
    }
    moduleList.addAll(outerDir.children)
}

// Remove the top-level modules which are empty and useless.
rootProject.children.clear()

// Add the subdirectories as top-level modules.
rootProject.children.addAll(moduleList)
